inv := invocation_directory()

default:
	for f in */main.idr; do echo "${f%%/*}"; done | xargs -P 0 -n 1 just build

check dir=inv:
	idris2 -c {{dir / "main.idr"}}
alias c := check

build dir=inv:
	idris2 --build-dir {{dir / "out"}} --output-dir {{dir / "out"}} -o main {{dir / "main.idr"}}
alias b := build

run dir=inv: (build dir)
	cd {{dir}} && {{dir / "out" / "main"}}
alias r := run

clean:
	rm -rf */out build
alias cl := clean

repl dir=inv:
	cd {{dir}} && rlwrap idris2 {{dir / "main.idr"}}
alias re := repl
